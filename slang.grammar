Grammar: SLang

Terminals Start
WHITESPACE: "([ \n\t])*" IGNORE
LPAREN: "\("
RPAREN: "\)"
COMMA: ","
COLON: ":"
SEMICOLON: ";"
ADD: "+"
SUB: "-"
MUL: "*"
DIV: "/"
NOT: "~"
EQUAL: "="
LT: "<"
ANDOP: "&&"
OROP: "||"
WHAT: "?"
UNIT: "\(\)"
TRUE: "[tT]rue"
FALSE: "[fF]alse"
IF: "if"
THEN: "then"
ELSE: "else"
LET: "let"
IN: "in"
BEGIN: "begin"
END: "end"
BOOL: "bool"
INTTYPE: "int"
UNITTYPE: "unit"
ARROW: "->"
BAR: "|"
INL: "inl"
INR: "inr"
FST: "fst"
SND: "snd"
FUN: "fun"
CASE: "case"
OF: "of"
REF: "ref"
ASSIGN: ":="
BANG: "!"
WHILE: "while"
DO: "do"
INT: "[0-9]([0-9])*" STORE
IDENT: "[a-zA-Z]([a-zA-Z0-9_'])*" STORE
Terminals End

NonTerminals Start
EXPR
EXPR1
SUM
INTERMEDIATE_BINARY_OP
ASSIGNMENT
SIMPLE_EXPR
EXPR_LIST
TYPE
TYPE1
NonTerminals End

Productions Start
EXPR -> EXPR1
      | begin EXPR_LIST end
      | if EXPR then EXPR else EXPR ;
      | while EXPR do EXPR ;
      | fst ( EXPR )
      | snd ( EXPR )
      | inl TYPE ( EXPR )
      | inr TYPE ( EXPR )
      | fun ( IDENT : TYPE ) -> ( EXPR )
      | let IDENT : TYPE = EXPR in ( EXPR )
      | let IDENT ( IDENT : TYPE ) : TYPE = EXPR in ( EXPR )
      | case EXPR of inl ( IDENT : TYPE ) -> EXPR | inr ( IDENT : TYPE ) -> EXPR ;

EXPR1 -> - SIMPLE_EXPR
       | SUM
       | EXPR SIMPLE_EXPR

SUM -> SUM + INTERMEDIATE_BINARY_OP
     | SUM - INTERMEDIATE_BINARY_OP
     | INTERMEDIATE_BINARY_OP

INTERMEDIATE_BINARY_OP -> INTERMEDIATE_BINARY_OP * ASSIGNMENT
	 | INTERMEDIATE_BINARY_OP / ASSIGNMENT
	 | INTERMEDIATE_BINARY_OP < ASSIGNMENT
	 | INTERMEDIATE_BINARY_OP = ASSIGNMENT
	 | INTERMEDIATE_BINARY_OP && ASSIGNMENT
	 | INTERMEDIATE_BINARY_OP || ASSIGNMENT
	 | ASSIGNMENT

ASSIGNMENT -> ASSIGNMENT := ( EXPR )
            | SIMPLE_EXPR

SIMPLE_EXPR -> ()
             | INT
             | ?
             | IDENT
             | TRUE
             | FALSE
             | ( EXPR )
             | ( EXPR , EXPR )
             | ~ ( EXPR )
             | ! ( EXPR )
             | ref ( EXPR )

EXPR_LIST -> EXPR
           | EXPR ; EXPR_LIST

TYPE -> TYPE -> TYPE1
      | TYPE * TYPE1
      | TYPE + TYPE1
      | TYPE ref
      | TYPE1

TYPE1 -> ( TYPE )
       | bool
       | int
       | unit

Productions End

Start Symbol: EXPR
